attacker[active]

principal Alice[
  knows public salt, ci, cr
  knows private da
  knows private ika
  knows private eka
  pika = G^ika
  peka = G^eka
]

principal Bob[
  knows public salt, ci, cr
  knows private db
  knows private ikb
  knows private ekb
  pikb = G^ikb
  pekb = G^ekb
]

Alice -> Bob: [pika], peka

principal Bob[
  tb = CONCAT(pika, pikb, peka, pekb)
  sb = SIGN(ikb, tb)
  kmb = peka^ekb
  skab = HKDF(salt, kmb, ci)
  skbb = HKDF(salt, kmb, cr)
  hb = AEAD_ENC(skbb, sb, nil)
]

Bob -> Alice: [pikb], pekb, hb

principal Alice[
  ta = CONCAT(pika, pikb, peka, pekb)
  kma = pekb^eka
  skba = HKDF(salt, kma, cr)
  sba = AEAD_DEC(skba, hb, nil)?
  _ = SIGNVERIF(pikb, ta, sba)?
  sa = SIGN(ika, ta)
  skaa = HKDF(salt, kma, ci)
  ha = AEAD_ENC(skaa, sa, nil)
]

Alice -> Bob: ha

principal Bob[
  sab = AEAD_DEC(skab, ha, nil)?
  _ = SIGNVERIF(pika, tb, sab)?
  mb = AEAD_ENC(skbb, db, nil)
]

Bob -> Alice: mb

principal Alice[
  _ = AEAD_DEC(skba, cb, nil)?
  ma = AEAD_ENC(skaa, da, nil)
]

Alice -> Bob: ma

principal Bob[
  _ = AEAD_DEC(skab, ma, nil)?
]

queries[
  authentication? Bob -> Alice: mb
  authentication? Alice -> Bob: ma
  confidentiality? da
  confidentiality? db
]
